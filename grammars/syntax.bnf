file = entry* table*

table = head entry*
head = "[" keypath "]" NL | "[[" keypath "]]" NL
keypath = ( key "." )* key
entry = keypath "=" value? NL

value = ( tag "@" )? tagless
tagless = array | object | number | string | boolean | typed | raw
typed = type raw # note for parser implementor: type and open par must be adjacent, no spaces or newlines allowed!
raw = "(" (ANY - NL)* ")" # note than () [] {} parens must be balanced inside raw string
array = "[" value ( ","* value )* ","* "]"
object = "{" keyvalue ( ","* keyvalue )* ","* "}"
keyvalue = key ":" value
boolean = "true" | "false" | "yes" | "no" | "on" | "off"

tag = IDENT
type = IDENT
key = IDENT | string | number
number = FLOAT | INT
string = SINGLE_STRING | DOUBLE_STRING | TRIPLE_SINGLE_STRING | TRIPLE_DOUBLE_STRING
