file = entries table*

table = head entries
entries = ( entry ( entrysep entry )* entrysep? )?
entrysep = NL | ";"
head = "[" keypath "]" NL | "[[" keypath "]]" NL
keypath = ( key "." )* key
entry = keypath "=" expr? NL

expr = app | const
app = func "()" | func "(" expr ( "," expr )* ","? ")" | func const # note no space between ident and next token allowed!
const = array | object | number | string | boolean
array = "[" expr ( "," expr )* ","? "]"
object = "{" keyexpr ( "," keyexpr )* ","? "}"
keyexpr = key ":" expr
boolean = "true" | "false" | "yes" | "no" | "on" | "off" | "T" | "F" | "Y" | "N"

func = IDENT
key = IDENT | string | number
number = FLOAT | DECINT | HEXINT | OCTINT | BININT
string = SINGLE_STRING | DOUBLE_STRING | TRIPLE_SINGLE_STRING | TRIPLE_DOUBLE_STRING
